configurations {
    core
    junitAnt
}

dependencies {
    junitAnt 'junit:junit:4.8.2'
    junitAnt('org.apache.ant:ant-junit:1.9.2') {
        transitive = false
    }
    junitAnt('org.apache.ant:ant-junit4:1.9.2') {
        transitive = false
    }
}

ant.taskdef(name: 'junit',
    classname: 'org.apache.tools.ant.taskdefs.optional.junit.JUnitTask',
    classpath: configurations.junitAnt.asPath)
ant.taskdef(name: 'junitreport',
    classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
    classpath: configurations.junitAnt.asPath)

ant.importBuild("$projectDir/build.xml") { antTargetName ->
    'ant-core-' + antTargetName
}

task antCompile(type: Jar) {
    doLast {
        ant.project.executeTarget('compile')
    }
}

artifacts {
    core antCompile
    core file("$coreDir/.build/target/${platform}/jmy.jar")
}

build {
    dependsOn antCompile
}

clean {
    doLast {
        ant.project.executeTarget('clean')
    }
}
